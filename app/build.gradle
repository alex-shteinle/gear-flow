buildscript {
    apply from: '../deps.gradle'
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}

static def dateTime() {
    return new Date().format("dd.MM.yyyy_HH.mm")
}

android {
    namespace 'ls.gear.flow'
    compileSdk build_versions.compile_sdk

    defaultConfig {
        applicationId "ls.gear.flow"
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        archivesBaseName = "GearFlow-$versionName-${dateTime()}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    coreLibraryDesugaring deps.desugar

    implementation project(':domain')
    implementation project(':data')

    implementation deps.kotlinx_immutable

    // DI
    implementation deps.koin.android
    implementation deps.koin.compose

    // AndroidX
    implementation deps.androidx.core
    implementation deps.androidx.lifecycle_runtime_ktx
    implementation deps.androidx.lifecycle_compose
    implementation deps.androidx.activity_compose
    implementation deps.androidx.biometric

    // Compose
    implementation platform(deps.androidx.compose.bom)
    implementation deps.androidx.compose.ui
    implementation deps.androidx.compose.ui_graphics
    implementation deps.androidx.compose.ui_tooling_preview
    implementation deps.androidx.compose.material
    implementation deps.androidx.compose.material3
    implementation deps.androidx.compose.icons_ext
    implementation deps.androidx.compose.navigation
    implementation deps.accompanist.nav_animation
    implementation deps.accompanist.system_ui_controller

    // Testing
    testImplementation deps.test.junit
    testImplementation deps.koin.test
    testImplementation deps.koin.test_junit
    androidTestImplementation platform(deps.androidx.compose.bom)
    androidTestImplementation deps.androidx.compose.ui_test_junit4
    debugImplementation deps.androidx.compose.ui_tooling
    debugImplementation deps.androidx.compose.ui_test_manifest
}
